import os

# To run locally:
# snakemake --snakefile Snakefile_estimate --keep-going --cores 7 --use-singularity --singularity-prefix ~/.singularity --singularity-args "--home ~"

# To visualise the pipeline:
# snakemake --snakefile Snakefile_estimate --dag | dot -Tsvg > pipeline_estimate.svg

localrules: all

folder = os.path.abspath(config.get("folder", '.'))
ebola_folder = os.path.join(folder, 'data', 'SLE')
IS=range(10)

rule all:
    input:
        os.path.join(ebola_folder, 'estimates.tab'),


rule count_tips:
    '''
    Counts the number of tips in the forest
    '''
    input:
        nwk = os.path.join(ebola_folder, '{tree}.nwk'),
    output:
        log = os.path.join(ebola_folder, '{tree}.tips'),
    params:
        mem = 2000,
        name = 'tips',
        qos = 'tips'
    threads: 1
    singularity: "docker://evolbioinfo/pastml:v1.9.40"
    shell:
        """
        python3 py/count_tips.py --in_nwk {input} --out_log {output}
        """

rule estimate_p:
    '''
    Estimate parameters on the forest.
    '''
    input:
        nwk = os.path.join(ebola_folder, 'SLE.bottom.{i}.days.nwk'),
        log = os.path.join(ebola_folder, 'SLE.bottom.{i}.days.tips'),
    output:
        est = os.path.join(ebola_folder, 'SLE.u=estimated.{i,[0-9]+}.p=real.est'),
        est_more = os.path.join(ebola_folder, 'SLE.u=estimated.{i}.p=higher.est'),
        est_less = os.path.join(ebola_folder, 'SLE.u=estimated.{i}.p=lower.est'),
        time = os.path.join(ebola_folder, 'SLE.u=estimated.{i,[0-9]+}.p=real.time'),
        time_more=os.path.join(ebola_folder,'SLE.u=estimated.{i,[0-9]+}.p=higher.time'),
        time_less=os.path.join(ebola_folder,'SLE.u=estimated.{i,[0-9]+}.p=lower.time'),
    params:
        mem = 2000,
        name = 'estimate_{i}',
        qos = 'fast'
    threads: 2
    singularity: "docker://evolbioinfo/bdei:v0.7"
    shell:
        """
        t=`more {input.log}`
        p=`echo "print($t / (13683 - 533))" | python3`
        
        bdei_infer --p $p --threads {threads} -c 100 --nwk {input.nwk} \
        --log {output.est} --time_log {output.time} --T 0 --upper_bounds 1 50 50 0.5
        
        
        p=`echo "print($t / (0.8 * (13683 - 533)))" | python3`
        
        bdei_infer --p $p --threads {threads} -c 100 --nwk {input.nwk} \
        --log {output.est_more} --time_log {output.time_more} --T 0 --upper_bounds 1 50 50 0.5
        
        
        p=`echo "print($t / (1.2 * (13683 - 533)))" | python3`
        bdei_infer --p $p --threads {threads} -c 100 --nwk {input.nwk} \
        --log {output.est_less} --time_log {output.time_less} --T 0 --upper_bounds 1 50 50 0.5
        """


rule estimate_p_u_fixed:
    '''
    Estimate parameters on the forest, having u fixed.
    '''
    input:
        nwk=os.path.join(ebola_folder,'SLE.bottom.{i}.days.nwk'),
        log=os.path.join(ebola_folder,'SLE.bottom.{i}.days.tips'),
    output:
        est=os.path.join(ebola_folder,'SLE.u=fixed.{i,[0-9]+}.p=real.est'),
        est_more=os.path.join(ebola_folder,'SLE.u=fixed.{i}.p=higher.est'),
        est_less=os.path.join(ebola_folder,'SLE.u=fixed.{i}.p=lower.est'),
        time=os.path.join(ebola_folder,'SLE.u=fixed.{i,[0-9]+}.p=real.time'),
        time_more=os.path.join(ebola_folder,'SLE.u=fixed.{i,[0-9]+}.p=higher.time'),
        time_less=os.path.join(ebola_folder,'SLE.u=fixed.{i,[0-9]+}.p=lower.time'),
    params:
        mem=2000,
        name='estimate_{i}_u',
        qos='fast'
    threads: 2
    singularity: "docker://evolbioinfo/bdei:v0.7"
    shell:
        """
        t=`more {input.log}`
        p=`echo "print($t / (13683 - 533))" | python3`
        n=`grep ";" {input.nwk} | wc -l`
        u=`echo "print(533 - $n)" | python3`

        bdei_infer --p $p --threads {threads} -c 100 --nwk {input.nwk} \
        --log {output.est} --time_log {output.time} --T 0 --upper_bounds 1 50 50 0.5 --u $u


        p=`echo "print($t / (0.8 * (13683 - 533)))" | python3`

        bdei_infer --p $p --threads {threads} -c 100 --nwk {input.nwk} \
        --log {output.est_more} --time_log {output.time_more} --T 0 --upper_bounds 1 50 50 0.5 --u $u


        p=`echo "print($t / (1.2 * (13683 - 533)))" | python3`
        bdei_infer --p $p --threads {threads} -c 100 --nwk {input.nwk} \
        --log {output.est_less} --time_log {output.time_less} --T 0 --upper_bounds 1 50 50 0.5 --u $u
        """


rule estimate_p_u_zero:
    '''
    Estimate parameters on the forest, having u fixed to zero.
    '''
    input:
        nwk=os.path.join(ebola_folder,'SLE.bottom.{i}.days.nwk'),
        log=os.path.join(ebola_folder,'SLE.bottom.{i}.days.tips'),
    output:
        est=os.path.join(ebola_folder,'SLE.u=0.{i,[0-9]+}.p=real.est'),
        est_more=os.path.join(ebola_folder,'SLE.u=0.{i}.p=higher.est'),
        est_less=os.path.join(ebola_folder,'SLE.u=0.{i}.p=lower.est'),
        time=os.path.join(ebola_folder,'SLE.u=0.{i,[0-9]+}.p=real.time'),
        time_more=os.path.join(ebola_folder,'SLE.u=0.{i,[0-9]+}.p=higher.time'),
        time_less=os.path.join(ebola_folder,'SLE.u=0.{i,[0-9]+}.p=lower.time'),
    params:
        mem=2000,
        name='estimate_{i}_u',
        qos='fast'
    threads: 2
    singularity: "docker://evolbioinfo/bdei:v0.7"
    shell:
        """
        t=`more {input.log}`
        p=`echo "print($t / (13683 - 533))" | python3`

        bdei_infer --p $p --threads {threads} -c 100 --nwk {input.nwk} \
        --log {output.est} --time_log {output.time} --T 0 --upper_bounds 1 50 50 0.5 --u 0


        p=`echo "print($t / (0.8 * (13683 - 533)))" | python3`

        bdei_infer --p $p --threads {threads} -c 100 --nwk {input.nwk} \
        --log {output.est_more} --time_log {output.time_more} --T 0 --upper_bounds 1 50 50 0.5 --u 0


        p=`echo "print($t / (1.2 * (13683 - 533)))" | python3`
        bdei_infer --p $p --threads {threads} -c 100 --nwk {input.nwk} \
        --log {output.est_less} --time_log {output.time_less} --T 0 --upper_bounds 1 50 50 0.5 --u 0
        """

rule combine_estimates:
    '''
    Combine estimates.
    '''
    input:
        nwk = expand(os.path.join(ebola_folder, 'SLE.bottom.{i}.days.nwk'), i=IS),
        est = expand(os.path.join(ebola_folder, 'SLE.u={u}.{i}.p={p}.est'), i=IS,
            p=['real', 'lower', 'higher'],
            u=['fixed', 'estimated', '0']),
        times = expand(os.path.join(ebola_folder, 'SLE.u={u}.{i}.p={p}.time'), i=IS,
            p=['real', 'lower', 'higher'],
            u=['fixed', 'estimated', '0']),
    output:
        tab = os.path.join(ebola_folder, 'estimates.tab'),
        time = os.path.join(ebola_folder, 'estimates.time'),
    params:
        mem = 2000,
        name = 'estimates',
        qos = 'fast',
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/summary_table.py --forests {input.nwk} --estimates {input.est} --tab {output.tab} --times {input.times} --time {output.time}
        """
