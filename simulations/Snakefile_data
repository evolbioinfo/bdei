import os

# To run locally:
# snakemake  --snakefile Snakefile_data --keep-going --cores 4 --use-singularity --singularity-prefix ~/.singularity --singularity-args "--home ~"

# To run on bioevo (change shakemake activation below if you are not Anna ;) :
# source ~/.bashrc && conda activate snakemake && module load singularity/3.8.5 && cd /pasteur/zeus/projets/p01/Evolbioinfo/users/azhukova/projects/bdei/simulations
# snakemake --snakefile Snakefile_data --keep-going --cores 1 --use-singularity --singularity-prefix $HOME/.singularity --singularity-args "-B /pasteur" --cluster "sbatch -c {threads} -o logs/{params.name}.log -e logs/{params.name}.log --mem {params.mem} -p bioevo --qos={params.qos}  -A bioevo -J {params.name}" --jobs 300


localrules: all

os.makedirs('logs', exist_ok=True)
sim_folder = os.path.abspath('.')
DATASETS = ['medium', 'large']
REPETITIONS = list(range(100))


rule all:
    input:
        expand(os.path.join(sim_folder, 'large', 'introductions', 'introduction.{i}.nwk'), i=range(100)),
        expand(os.path.join(sim_folder, 'large', 'subepidemics', 'subepidemic.{i}.nwk'), i=range(100)),
        expand(os.path.join(sim_folder, 'large', 'forests', 'forest.{i}.nwk'), i=range(100)),
        expand(os.path.join(sim_folder, '{dataset}', 'trees', 'tree.{i}.nwk'), i=range(100), dataset=DATASETS),


rule prepare_phylodeep:
    '''
    Parses PhyloDeep data.
    '''
    input:
        nwk = os.path.join(sim_folder, '{dataset}', 'trees.nwk'),
        log = os.path.join(sim_folder, '{dataset}', 'TARGET.csv'),
    output:
        nwk = expand(os.path.join(sim_folder, '{{dataset}}', 'trees', 'tree.{i}.nwk'), i=range(100)),
        log = expand(os.path.join(sim_folder, '{{dataset}}', 'trees', 'tree.{i}.log'), i=range(100)),
    params:
        mem = 2000,
        name = 'trees_{dataset}',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/split_trees.py --in_nwk {input.nwk} --in_log {input.log} --out_nwks {output.nwk} --out_logs {output.log}
        """

rule prepare_clusters_large:
    '''
    Splits the tree with PhyloDeep's cluster picker.
    '''
    input:
        nwk = os.path.join(sim_folder, 'large', 'trees', 'tree.{i}.nwk'),
        log = os.path.join(sim_folder, 'large', 'trees', 'tree.{i}.log'),
    output:
        nwk = os.path.join(sim_folder, 'large', 'clusters', 'cluster.{i}.nwk'),
        log = os.path.join(sim_folder, 'large', 'clusters', 'cluster.{i}.log'),
    params:
        mem = 2000,
        name = 'cluster_{i}',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/phylodeep:v0.2.61"
    shell:
        """
        subtree_picker -i {input.nwk} -o {output.nwk} -m 50 -M 500

        mu=`tail -n 1 {input.log} | awk '{{ print $5 }}'`
        la=`tail -n 1 {input.log} | awk '{{ print $6 }}'`
        psi=`tail -n 1 {input.log} | awk '{{ print $7 }}'`
        p=`tail -n 1 {input.log} | awk '{{ print $8 }}'`
        f=`grep -o ';' {output.nwk}  | wc -l`
        echo "tips	f	u	T	mu	la	psi	p" > {output.log}
        echo "NA	$f	0	0	$mu	$la	$psi	$p" >> {output.log}
        """

rule prepare_forests_large:
    '''
    Cuts large trees removing the top X%.
    '''
    input:
        nwk = os.path.join(sim_folder, 'large', 'trees', 'tree.{i}.nwk'),
        log = os.path.join(sim_folder, 'large', 'trees', 'tree.{i}.log'),
    output:
        nwk = os.path.join(sim_folder, 'large', 'forests', 'forest.{i}.nwk'),
        log = os.path.join(sim_folder, 'large', 'forests', 'forest.{i}.log'),
    params:
        mem = 2000,
        name = 'forest_{i}',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/cut_tree.py --in_nwk {input.nwk} --in_log {input.log} --out_nwk {output.nwk} --out_log {output.log} \
        --threshold 0.25
        """


rule prepare_subemidemics_large:
    '''
    Extract multiple introductions from the tree.
    '''
    input:
        nwk = os.path.join(sim_folder, 'large', 'trees', 'tree.{i}.nwk'),
        log = os.path.join(sim_folder, 'large', 'trees', 'tree.{i}.log'),
    output:
        nwk = os.path.join(sim_folder, 'large', 'introductions', 'introduction.{i}.nwk'),
        log = os.path.join(sim_folder, 'large', 'introductions', 'introduction.{i}.log'),
    params:
        mem = 2000,
        name = 'introduction_{i}',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/python-evol:v3.6richer.1"
    shell:
        """
        python3 py/cut_subepidemics.py --in_nwk {input.nwk} --in_log {input.log} --out_nwk {output.nwk} --out_log {output.log} \
        --min_T 0 --max_T 1 --min_tips 5000 
        """

rule simulate:
    '''
    Simulate a forest for given parameters.
    '''
    input:
        log = os.path.join(sim_folder, 'large', 'trees', 'tree.{i}.log'),
    output:
        nwk = os.path.join(sim_folder, 'large', 'subepidemics', 'subepidemic.{i}.nwk'),
        log = os.path.join(sim_folder, 'large', 'subepidemics', 'subepidemic.{i}.log'),
    params:
        mem = 2000,
        name = 'simulate_{i}',
        qos = 'fast'
    threads: 1
    singularity: "docker://evolbioinfo/treesimulator:v0.1.6"
    shell:
        """
        python py/forest_generator.py --min_tips 5000 --max_tips 10000 \
        --in_log {input.log} --nwk {output.nwk} --out_log {output.log} \
        --min_T 0 --max_T 1
        """